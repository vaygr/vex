# Maintainer: Alexander F. RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Bastien Dejean <nihilhill@gmail.com>

_pkgbase=bspwm
pkgname=bspwm-vex
pkgver=0.9.10
pkgrel=4
pkgdesc='Tiling window manager based on binary space partitioning'
arch=(x86_64)
url='https://github.com/baskerville/bspwm'
license=(BSD)
makedepends=(git)
depends=(xcb-util xcb-util-wm xcb-util-keysyms)
optdepends=('sxhkd: to define keyboard and pointer bindings'
            'xdo: for the example panel')
provides=("${_pkgbase}")
conflicts=("${_pkgbase}" "${_pkgbase}-git")
source=("git+$url#commit=2492695ad47c7ae8c630bdf22328334f57e41c1a" # tag: 0.9.10
        "floating.patch")
md5sums=('SKIP'
         '33493ab34e1c6a897f5eea8a97fdd853')

prepare() {
  cd ${_pkgbase}

  patch -p1 < ../floating.patch
}

build() {
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  make -C "${_pkgbase}" PREFIX=/usr
}

package() {
  cd "${_pkgbase}"

  make PREFIX=/usr DESTDIR="$pkgdir" install

  # BSD 2-clause
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/${_pkgbase}/LICENSE"
}

# getver: -u 2 raw.githubusercontent.com/baskerville/bspwm/master/doc/bspwm.1
# vim: ts=2 sw=2 et:
