# Maintainer: Val V <scm(at)vaygr(dot)net>

_pkgbase=dmenu
pkgname=dmenu-vex
pkgver=5.3
pkgrel=1
pkgdesc='Generic menu for X (VEX Edition)'
url='https://tools.suckless.org/dmenu/'
arch=('x86_64')
license=('MIT')
depends=('sh' 'glibc' 'coreutils' 'libx11' 'libxinerama' 'libxft' 'freetype2' 'fontconfig' 'libfontconfig.so')
makedepends=('patchutils')
provides=("${_pkgbase}")
conflicts=("${_pkgbase}")
source=(https://dl.suckless.org/tools/${_pkgbase}-${pkgver}.tar.gz
        https://tools.suckless.org/${_pkgbase}/patches/password/${_pkgbase}-password-4.9.diff
        https://tools.suckless.org/${_pkgbase}/patches/line-height/${_pkgbase}-lineheight-4.9.diff
        https://tools.suckless.org/${_pkgbase}/patches/mouse-support/${_pkgbase}-mousesupport-5.3.diff
        https://tools.suckless.org/${_pkgbase}/patches/numbers/${_pkgbase}-numbers-20220512-28fb3e2.diff
        https://tools.suckless.org/${_pkgbase}/patches/xyw/${_pkgbase}-xyw-5.0.diff
        https://tools.suckless.org/${_pkgbase}/patches/fuzzymatch/${_pkgbase}-fuzzymatch-4.9.diff
        https://tools.suckless.org/${_pkgbase}/patches/fuzzyhighlight/${_pkgbase}-fuzzyhighlight-4.9.diff
        https://tools.suckless.org/${_pkgbase}/patches/xresources-alt/${_pkgbase}-xresources-alt-5.0.diff
        password.patch
        xresources-alt.patch
        xresources-alt_fhl.patch
        fuzzymatch_xr.patch
        fuzzyhighlight.patch
        fuzzyhighlight_xr.patch)
sha512sums=('60f758134063ad7f304fbb0785c66dddc21461e5e5014f8735cf6d5c95dc22a177f069d48101e633ddffe5bbefc1118a2bf8b31daaf7b6438fdf6fe1bcb5d61c'
            '455dfa017585962d64dbcb0a091c32809699f0e067ad28c94cc3bf86f4e6801b996bd4bac71ce15a5255114de5bdedc676b62fb76cb1b9f71be33017af1c72be'
            'b2fa2b33b9a2391285d0c3e5df22a9d68f4193bebb4720ce71678e8a87f43ae6c0a6b3f3fd3f63aa89be95880c04c8a83e43240b6ae4f90487bdb5a9325d084c'
            'a78c49c89e73bb0570b46c1282c8327d453cddc435806bb2bbc5ce8f3c64b6978b3cd12cd56e1e3d7f3b49462cbcf0d2f9a29ce30a5d940893fb6a9814236918'
            'ff851cc4ff38f2bf8cad727a1dcc7a927b43aba1159ea14178a4aa894f83db715a7ef920183d7362110c9736299cc3c171a4d645c994f395dda6fd79fa5adc57'
            'af093f2e2be3c5c379add2340fcb9c4e63376a055849fe91685d033a486dae0b7d806b987b89498162ff00c3f47eaa0bda9601b08e5fdf4226af8bb18aa5057f'
            '6ce4ce0ddb9c1058f253ce46cdae8949c468d97ce7d787831d8a4f4ebd4db672761e2552703f58d36d01b2933dea624a30627629027b9a5960b404542a40a732'
            'ce806c6481b20d5b2352fc387816878c75285162f6404b3a969a95f9dbd6c7476fd4c6571f260886fa30ccc40e4a388ea8f6902e40ff90447e04bec3faf0669d'
            'ddd20d6c2f5e57fc8c60be53943d10656f6e41558750e28050ca23858aa0e7fe3b189788419e6a87cb24a85f0ab8c83a486320023117b4a4734a80882d1265a8'
            'e69d88c90d43e3442c2ca478aeb465d7dd67cb5e90966b3e83dcd132bac1fb5b6b7014c1b7a5f75caa7e951c5f3875da9a6a669c644368078d8084fc415b03da'
            '048ad0b4e4f667becff53a0d143d9746b57a5849cda4b2f187ff4a814064b8bcb9dced32b715ca0fb357aea040647046bc3135f6e2759871c1db03af20dc19f0'
            '6b7cad63784051c1c26fbcc3fb8eed6772af4cfdbbb15be575c5d2a35434d6cf8a3eba49232b78b909e9cf1105ce5187984730b6fe16efa3c981709212a667c7'
            'f3018aaaf0221e01fa6113c4d7d170812b0b14c102475f7f0bb3af07fcdb846afba6393b6d4b9a9ec38cbaf37a56c2f16c107b0e48005bb94c9ae5e20d8dc31f'
            'c491be2c90e40e333a3674a9be1af918125d20cc7933125424a1a0e3f2c6a93b32657c4d22e35d44337fdbbc100d1837a50d8b6e36bfb2088f1c5c84e56200e0'
            '91fd0c1119b87e9462d9859f98839c9068cdba9244f7b53deabd282e3f775422f90093a53ad50fba89a18c98a649522e3138afde55aa6e31b655e94dca3b080f')

prepare() {
  cd ${_pkgbase}-${pkgver}

  interdiff ../dmenu-password-4.9.diff /dev/null > dmenu-password-4.9.diff
  patch -p0 < ../password.patch
  patch -p1 < dmenu-password-4.9.diff
  sed "s:@@ -683,6 +684,7 @@:@@ -686,6 +687,7 @@:;s:-bfiv:-bfiPv:g" ../dmenu-lineheight-4.9.diff | patch -p1 -F3 # ignore context in case previous patch was applied
  cp ../dmenu-xresources-alt-5.0.diff .
  patch -p0 < ../xresources-alt.patch
  patch -p0 < ../xresources-alt_fhl.patch
  patch -p1 < dmenu-xresources-alt-5.0.diff
  patch -p1 < ../dmenu-mousesupport-5.3.diff
  patch -p1 < ../dmenu-numbers-20220512-28fb3e2.diff
  sed "s:@@ -690,6 +693,7 @@:@@ -694,6 +696,7 @@:" ../dmenu-xyw-5.0.diff | patch -p1 -F3 # ignore context in case password/lineheight patch was applied
  cp ../dmenu-fuzzymatch-4.9.diff .
  patch -p0 < ../fuzzymatch_xr.patch
  patch -p1 < dmenu-fuzzymatch-4.9.diff
  sed -i "s:-bfi:-bfiF:" dmenu.c
  cp ../dmenu-fuzzyhighlight-4.9.diff .
  patch -p0 < ../fuzzyhighlight.patch
  patch -p0 < ../fuzzyhighlight_xr.patch
  patch -p1 -F3 < dmenu-fuzzyhighlight-4.9.diff

  echo "CPPFLAGS+=${CPPFLAGS}" >> config.mk
  echo "CFLAGS+=${CFLAGS}" >> config.mk
  echo "LDFLAGS+=${LDFLAGS}" >> config.mk
}

build() {
  cd ${_pkgbase}-${pkgver}
  make \
	  X11INC=/usr/include/X11 \
	  X11LIB=/usr/lib/X11 \
	  FREETYPEINC=/usr/include/freetype2
}

package() {
  cd ${_pkgbase}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${_pkgbase}"
}

# vim: ts=2 sw=2 et:
