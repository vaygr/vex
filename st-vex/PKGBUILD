# Maintainer: Vlad Glagolev <scm(at)vaygr(dot)net>

_pkgbase=st
pkgname=st-vex
pkgver=0.8.5
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X (VEX Edition)'
arch=('i686' 'x86_64' 'armv7h' 'aarch64')
license=('MIT')
depends=(libxft)
provides=("${_pkgbase}")
conflicts=("${_pkgbase}")
url=https://st.suckless.org
source=(https://dl.suckless.org/${_pkgbase}/${_pkgbase}-${pkgver}.tar.gz
        terminfo.patch
        README.terminfo.rst
        https://st.suckless.org/patches/alpha/${_pkgbase}-alpha-20220206-${pkgver}.diff
        https://st.suckless.org/patches/blinking_cursor/${_pkgbase}-blinking_cursor-20211116-2f6e597.diff
        https://st.suckless.org/patches/scrollback/${_pkgbase}-scrollback-${pkgver}.diff
        https://st.suckless.org/patches/scrollback/${_pkgbase}-scrollback-mouse-20220127-2c5edf2.diff
        https://st.suckless.org/patches/scrollback/${_pkgbase}-scrollback-mouse-altscreen-20220127-2c5edf2.diff
        https://st.suckless.org/patches/xresources-with-reload-signal/${_pkgbase}-xresources-signal-reloading-20220407-ef05519.diff
        xsetcursor.patch)
sha256sums=('ea6832203ed02ff74182bcb8adaa9ec454c8f989e79232cb859665e2f544ab37'
            'f9deea445a5c6203a0e8e699f3c3b55e27275f17fb408562c4dd5d649edeea23'
            '0ebcbba881832adf9c98ce9fe7667c851d3cc3345077cb8ebe32702698665be2'
            '42e4803ce2a67835f7e533a707a8a28e3804a26ced163145108970b9aee5fb81'
            '1b62ce4dbe25a4dc3e8b350cd5bbaa2097899ff2690774e895bbc8ce0756df5b'
            'dc7f5223b26fc813d91d4ae35bdaa54d63024cae9f18afd9b3594ba3399dfa55'
            '46ac9bcdbfeb0011533207cb0ab31657a3eb9196da1d0db346e6a9d1fc4b4f76'
            '8f2f17683f12d57b1c80461247fe15234f5f5a6fc52cdf48176c8358e699101d'
            '89e447a2f7b14db1fa33819ed6ea70096f7e3b68cba54b3e159b8db2b50e4bc2'
            '11973136a8940b93a08590409034b6f79b24079af5ddb132114fce9f55a4435e')
_sourcedir="${_pkgbase}-${pkgver}"
_makeopts="--directory=$_sourcedir"

prepare() {
  patch --directory="$_sourcedir" --strip=0 < terminfo.patch

  # This package provides a mechanism to provide a custom config.h. Multiple
  # configuration states are determined by the presence of two files in
  # $BUILDDIR:
  #
  # config.h  config.def.h  state
  # ========  ============  =====
  # absent    absent        Initial state. The user receives a message on how
  #                         to configure this package.
  # absent    present       The user was previously made aware of the
  #                         configuration options and has not made any
  #                         configuration changes. The package is built using
  #                         default values.
  # present                 The user has supplied his or her configuration. The
  #                         file will be copied to $srcdir and used during
  #                         build.
  #
  # After this test, config.def.h is copied from $srcdir to $BUILDDIR to
  # provide an up to date template for the user.
  if [ -e "$BUILDDIR/config.h" ]
  then
    cp "$BUILDDIR/config.h" "$_sourcedir"
  elif [ ! -e "$BUILDDIR/config.def.h" ]
  then
    msg='This package can be configured in config.h. Copy the config.def.h '
    msg+='that was just placed into the package directory to config.h and '
    msg+='modify it to change the configuration. Or just leave it alone to '
    msg+='continue to use default values.'
    echo "$msg"
  fi
  cp "$_sourcedir/config.def.h" "$BUILDDIR"

  cd "${_sourcedir}"

  patch -p1 < ../st-alpha-20220206-0.8.5.diff
  patch -p1 < ../st-blinking_cursor-20211116-2f6e597.diff
  patch -p1 < ../st-scrollback-0.8.5.diff
  patch -p1 < ../st-scrollback-mouse-20220127-2c5edf2.diff
  patch -p1 < ../st-scrollback-mouse-altscreen-20220127-2c5edf2.diff
  sed "s:XRESOURCE_LOAD_INTEGER(\"cursorshape\", cursorshape);:XRESOURCE_LOAD_INTEGER(\"cursorstyle\", cursorstyle); XRESOURCE_LOAD_FLOAT(\"alpha\", alpha);:" ../st-xresources-signal-reloading-20220407-ef05519.diff | patch -p1
  patch -p0 < ../xsetcursor.patch
}

build() {
  make $_makeopts X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  local installopts='--mode 0644 -D --target-directory'
  local shrdir="$pkgdir/usr/share"
  local licdir="$shrdir/licenses/$pkgname"
  local docdir="$shrdir/doc/$pkgname"
  make $_makeopts PREFIX=/usr DESTDIR="$pkgdir" install
  install $installopts "$licdir" "$_sourcedir/LICENSE"
  install $installopts "$docdir" "$_sourcedir/README"
  install $installopts "$docdir" README.terminfo.rst
  install $installopts "$shrdir/${_pkgbase}" "$_sourcedir/st.info"
}
