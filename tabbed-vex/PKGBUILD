# Maintainer: Vlad Glagolev <scm(at)vaygr(dot)net>
# Contributor: Yusuf Aktepe <yusuf@yusufaktepe.com>
# Contributor: Lukas Fleischer <lfleischer@archlinux.org>
# Contributor: totoloco <totoloco at gmx>

_pkgbase=tabbed
pkgname=tabbed-vex
pkgver=0.7
pkgrel=6
pkgdesc='simple generic tabbed frontend to xembed-aware applications (VEX Edition)'
arch=('x86_64')
url='https://tools.suckless.org/tabbed'
license=('custom:MIT/X')
depends=('libx11' 'libxft')
provides=("${_pkgbase}")
conflicts=("${_pkgbase}")
install='tabbed.install'
source=(https://dl.suckless.org/tools/${_pkgbase}-${pkgver}.tar.gz
        config.h
        https://tools.suckless.org/${_pkgbase}/patches/alpha/alpha.diff
        https://tools.suckless.org/${_pkgbase}/patches/autohide/${_pkgbase}-autohide-20201222-dabf6a2.diff
        https://tools.suckless.org/${_pkgbase}/patches/xresources/${_pkgbase}-xresources-20230128-41e2b8f.diff
        https://tools.suckless.org/${_pkgbase}/patches/separator/${_pkgbase}-separator-20230128-41e2b8f.diff
        alpha.patch
        xresources.patch
        separator_xr.patch
        separator.patch)
sha256sums=('6e8682230a213d7dabf8a79306bd3ce023875b2295a9097db427d65c1c68f322'
            '2eb768bcf6474522b7765643318cb6b0178f3ad92fec3c869403db99b1a8cbaa'
            '920013a221c6f4fd834b52d2bf53dc20fc2d53912a9b1e311bc41f08979593f6'
            'bd38525d0ebcfb6c61984ec52b3b23184985075879c86d1e0b7e239c0ec2bce4'
            'a6c20e0efc52e11c2d9903aac205c3624cbf3c4eab97e62e189f77229173d57a'
            '6e8d115479e3dc34ef4d267be9881af71cf5454932c57f076b3938ec73764957'
            '4d7dee3c98253d7752dcbe270c7332b347675dd9d8a147c16c3da2c3eb5131c5'
            '76c62bb0a1909fed5252f8050fcdf8390cf12f222426a019959be6c509f47e59'
            '5418c29161f8300b810946900a632980a1be91c6a122a17b47b58ea028ab25f3'
            'e1b1daf30bfbf27ec403f44db180fef149b7ef00b9697c445fa5110aa608a8b8')

prepare() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  cp "${srcdir}/config.h" config.h

  sed -i 's/CPPFLAGS =/CPPFLAGS +=/g' config.mk
  sed -i 's/CFLAGS =/CFLAGS +=/g' config.mk
  sed -i 's/LDFLAGS =/LDFLAGS +=/g' config.mk
  sed -i '/xembed.1/d' Makefile

  cp ../alpha.diff .
  patch -p0 < ../alpha.patch
  patch -p1 < alpha.diff
  patch -p1 < ../tabbed-autohide-20201222-dabf6a2.diff
  patch -p1 < ../tabbed-xresources-20230128-41e2b8f.diff
  patch -p0 < ../xresources.patch
  cp ../tabbed-separator-20230128-41e2b8f.diff .
  patch -p0 < ../separator_xr.patch
  patch -p1 < tabbed-separator-20230128-41e2b8f.diff
  patch -p0 < ../separator.patch
}

build() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  make
}

package() {
  cd "${srcdir}/${_pkgbase}-${pkgver}"

  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm0644 LICENSE "${pkgdir}/usr/share/licenses/${_pkgbase}/LICENSE"
}
